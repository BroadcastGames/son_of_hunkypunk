apply plugin: 'com.android.application'

/*
Philosophy of moving more critical names into Gradle

This allows independent story apps to be build by authors and published on the Google Play store
without conflicting with the general-purpose Son of Hunky Punk app that is generically named and branded.

ToDo:
    1. Define a string for a 'startup game' and instead of a Game List go immediatly to the detail
       screen of that 'startup game' out of the assets folder.
 */

dependencies {
    //For some reason in 23.2 'GestureDetectorCompat' is already final which results in exception
    //in org.twview.ClickItemTouchListener.
    //Interestingly, below 23.0.0 the method 'RecyclerView.isAttachedToWindow()' is not recognized.
    //Luckily, v.up. >= 23.0.0 && v.up. < 23.2 work
    //compile 'com.android.support:recyclerview-v7:23.1.1'

    compile project(':library')

    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'

    compile 'org.greenrobot:eventbus:3.0.0'
}

// reference: https://blog.grandcentrix.net/how-to-install-different-app-variants-on-one-android-device
def final apkApplicationId = 'org.andglkmod.hunkypunk'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    // the applicationId is now decoupled http://tools.android.com/tech-docs/new-build-system/applicationid-vs-packagename
    defaultConfig {
        applicationId apkApplicationId
        minSdkVersion 14
        targetSdkVersion 24

        versionCode 14
        versionName "0.9.7"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "AUTHORITY", "\"org.andglkmod.hunkypunk.HunkyPunk\""

        resValue "string", "app_name",         "Son of Hunky Punk"
        resValue "string", "hunky_punk",       "Son of Hunky Punk"
        resValue "string", "config_authority", "org.andglkmod.hunkypunk.HunkyPunk"
    }

    sourceSets.main {
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        defaultFlavor {
            logger.warn('productFlavors defaultFlavor running')
        }

        development {
            versionNameSuffix '-BG1'
            applicationId = apkApplicationId + ".dev"

            buildConfigField "String", "AUTHORITY", "\"org.andglkmod.hunkypunk.dev.HunkyPunk\""

            // 0AA put at start of names so can be eaisly found in the list of App icons
            resValue "string", "app_name",         "0AADEV GrandSon of Hunky Punk"
            resValue "string", "hunky_punk",       "0AA GrandSon of Hunky Punk"
            resValue "string", "config_authority", "org.andglkmod.hunkypunk.dev.HunkyPunk"

            logger.warn('productFlavors running')
        }
    }

    println "    WHATIS version code to ${android.defaultConfig.versionCode}"
}
